&НаКлиенте
Процедура ФамилияПриИзменении(Элемент)

	СобратьФИО();

КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)

	СобратьФИО();

КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)

	СобратьФИО();

	УстановитьПолСотрудника();

	ОформитьПолеПолСотрудника();

КонецПроцедуры

&НаКлиенте
Процедура СобратьФИО()

	Объект.Наименование = Фамилия + " " + Имя + " " + Отчество;

	Заголовок = "Карточка сотрудника " + Объект.Наименование;

КонецПроцедуры // СобратьФИО()

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЧастиФИО = ПолучитьЧастиФИО(Объект.Наименование);

	Фамилия = ЧастиФИО.Фамилия;

	Имя = ЧастиФИО.Имя;

	Отчество = ЧастиФИО.Отчество;

	ОформитьПолеПолСотрудника();

КонецПроцедуры


Функция ПолучитьЧастиФИО(Знач ФИО)

	Если ТипЗнч(ФИО) = Тип("СправочникСсылка.Сотрудники") Тогда

		ФИО = ФИО.Наименование;

	КонецЕсли;

	ФИО = СокрЛП(ФИО);
	
	// Удаляем лишние пробелы внутри ФИО:

	Пока СтрЧислоВхождений(ФИО, "  ") > 0 Цикл

		ФИО = СтрЗаменить(ФИО, "  ", " ");

	КонецЦикла;

	СтруктураРезультат = Новый Структура;

	ФИО = СтрЗаменить(ФИО, " ", Символы.ПС);

	СтруктураРезультат.Вставить("Фамилия", СтрПолучитьСтроку(ФИО, 1));

	СтруктураРезультат.Вставить("Имя", СтрПолучитьСтроку(ФИО, 2));

	СтруктураРезультат.Вставить("Отчество", СтрПолучитьСтроку(ФИО, 3));

	Возврат СтруктураРезультат;

КонецФункции


&НаКлиенте
Процедура УстановитьПолСотрудника()

	ПоследняяБукваОтчетства = НРег(Прав(СокрЛП(Объект.Наименование), 1));

	Если ПоследняяБукваОтчетства = "ч" Тогда

		Объект.Пол = ПредопределенноеЗначение("Перечисление.Пол.Мужской");

	ИначеЕсли ПоследняяБукваОтчетства = "а" Тогда

		Объект.Пол = ПредопределенноеЗначение("Перечисление.Пол.Женский");

	Иначе

		Объект.Пол = ПредопределенноеЗначение("Перечисление.Пол.ПустаяСсылка");

	КонецЕсли;

КонецПроцедуры // УстановитьПолСотрудника()

&НаКлиенте
Процедура ОформитьПолеПолСотрудника()

	Если Объект.Пол = ПредопределенноеЗначение("Перечисление.Пол.Женский") Тогда

		Элементы.Пол.ЦветФона = WebЦвета.Розовый;

	ИначеЕсли Объект.Пол = ПредопределенноеЗначение("Перечисление.Пол.Мужской") Тогда

		Элементы.Пол.ЦветФона = WebЦвета.Голубой;

	Иначе

		Элементы.Пол.ЦветФона = WebЦвета.Оранжевый;

	КонецЕсли;

КонецПроцедуры // ОформитьПолеПолСотрудника()


&НаКлиенте
Процедура ЗагрузитьФотоИзФайла(Команда)

	НачатьПомещениеФайла(
	Новый ОписаниеОповещения("ЗагрузитьФотоИзФайлаЗавершение", ЭтотОбъект), , , Истина, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФотоИзФайлаЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт

	Если Результат Тогда

		ФотоСотрудника = Адрес;

	КонецЕсли;


КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(ФотоСотрудника) Тогда

		Фото = ПолучитьИзВременногоХранилища(ФотоСотрудника);

		ТекущийОбъект.Фото = Новый ХранилищеЗначения(Фото);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(ФотоСотрудника) Тогда

		УдалитьИзВременногоХранилища(ФотоСотрудника);

		ФотоСотрудника = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Фото");

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ФотоСотрудника = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Фото");

КонецПроцедуры